// Top-level build file where you can add configuration options common to all sub-projects/modules.

// Build directories location
def buildOutputRootDirectory = "${rootDir.parent}/__build-output/${rootProject.name}"
def gradleBuildOutputDirectory = "${buildOutputRootDirectory}/GradleBuilds"

String canonizePath(String path) {
    return file(path).canonicalPath.replace('\\', '/')
}

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath Deps.androidGradlePlugin

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

// The set containing this project and its subprojects.
allprojects {
    repositories {
        google()
        jcenter()
    }

    buildDir = canonizePath("${gradleBuildOutputDirectory}/${project.name}")
}

// This block encapsulates custom properties and makes them available to all
// modules in the project.
ext {
    // *.apk output directory
    apkExportDirectory = canonizePath("${rootDir.parent}/__apk-exports")
    // External libraries source location
    externalLibrariesDirectory = canonizePath("${rootDir.parent}/ExternalLibraries/")
    // Main project source location
    appProjectDirectory = canonizePath("${rootDir.parent}/GunBox/GunBox")
    // Location for library build artifacts
    nativeLibraryArtifactsOutputDirectory = canonizePath("${buildOutputRootDirectory}/__LibraryBuildArtifacts")
    // Build directories location
    nativeStagingDirectory = canonizePath("${buildOutputRootDirectory}/NativeBuilds/")
}

task clean(type: Delete) {
    delete rootProject.buildDir
    delete buildOutputRootDirectory
}

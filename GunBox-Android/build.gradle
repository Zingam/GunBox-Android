// Top-level build file where you can add configuration options common to all sub-projects/modules.

// Build directories location
def buildOutputRootDirectory = "${rootDir.parent}/__build-output/${rootProject.name}"
def gradleBuildOutputDirectory = "${buildOutputRootDirectory}/GradleBuilds"

String cannonizePath(String path) {
    return file(path).canonicalPath.replace('\\', '/')
}

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath Deps.androidGradlePlugin //'com.android.tools.build:gradle:3.2.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

// The set containing this project and its subprojects.
allprojects {
    repositories {
        google()
        jcenter()
    }

    buildDir = cannonizePath("${gradleBuildOutputDirectory}/${project.name}")
}

// This block encapsulates custom properties and makes them available to all
// modules in the project.
ext {
    // *.apk output directory
    apkOutputDirectory = cannonizePath("${file(buildOutputRootDirectory).parent}/__apk")
    // External libraries source location
    externalLibrariesDirectory = cannonizePath("${rootDir.parent}/ExternalLibraries/")
    // Main project source location
    appProjectDirectory = cannonizePath("${rootDir.parent}/GunBox/GunBox")
    // Location for library build artifacts
    nativeLibraryArtifactsOutputDirectory = cannonizePath("${buildOutputRootDirectory}/__LibraryBuildArtifacts")
    // Build directories location
    nativeStagingDirectory = cannonizePath("${buildOutputRootDirectory}/NativeBuilds/")
}

task clean(type: Delete) {
    delete rootProject.buildDir
    delete buildOutputRootDirectory
}

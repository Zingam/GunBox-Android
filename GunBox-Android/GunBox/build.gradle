apply plugin: 'com.android.application'

android {
    compileSdkVersion Versions.compileSdk



    defaultConfig {
        applicationId "com.roccoormm.sdl2app"
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode Versions.versionCode
        versionName Versions.versionName

        ndk {
            // Limiting to a smaller set of  ABIs to save time while testing:
            abiFilters = Deps.abiFilters
        }

        externalNativeBuild {
            cmake {
                // Passes optional arguments to CMake:
                //   External library sources root dir
                arguments "-D.ExternalLibrariesRootDir:STRING=${rootProject.ext.externalLibrariesDirectory}"
                //   Search paths for the external library binaries
                arguments "-D.LibraryArtifactsPath_FreeType2:STRING=${rootProject.ext.nativeLibraryArtifactsOutputDirectory}/${Deps.freetype2_ModuleName}",
                        "-D.LibraryArtifactsPath_SDL2:STRING=${rootProject.ext.nativeLibraryArtifactsOutputDirectory}/${Deps.sdl2_ModuleName}"
                //   Library type to link to
                arguments "-D.UseSharedLibrary_FreeType2:BOOL=${Deps.freetype2_BuildAsShared}",
                        // When linking a static SDL2 library to a shared
                        // library SDL_main gets stripped thus we compile all
                        // libraries as static and link them with:
                        //   $<$<PLATFORM_ID:Android>:-Wl,--whole-archive>
                        //   GunBox_Engine_main
                        //   $<$<PLATFORM_ID:Android>:-Wl,--no-whole-archive>
                        "-D.UseSharedLibrary_SDL2:BOOL=${Deps.sdl2_BuildAsShared}",
                        "-Doption_EngineLibraryAs_SHARED:BOOL=${Options.engineLibrary_BuildAsShared}"
                // Debug options
                arguments "-Doption_CheckGraphicsApiCalls:BOOL=${Options.checkGraphicsApiCalls}",
                        "-Doption_EnableLoggingLevel_Verbose:BOOL=${Options.enableLoggingLevel_Verbose}"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            // Tells Gradle to put outputs from external native
            // builds in the path specified below.
            buildStagingDirectory = "${rootProject.ext.nativeStagingDirectory}/${project.name}"

            // Tells Gradle to find the root CMake build script in the same
            // directory as the module's build.gradle file. Gradle requires this
            // build script to add your CMake project as a build dependency and
            // pull your native sources into your Android project.
            path "${rootProject.ext.appProjectDirectory}/CMakeLists.txt"
            version Versions.cmake
        }
    }

    // Specification of the copy tasks
    def export_apk_copySpec = copySpec {
        android.applicationVariants.all { variant ->
            variant.outputs.all { output ->
                from(output.outputFile.parent) {
                    include "*.apk"
                }
            }
        }
    }

    // Copy existing Android packages (*.apk)
    task copy_apk(type: Copy) {
        group "Export"
        with export_apk_copySpec
        into "${rootProject.ext.apkExportDirectory}"
    }

    // Build all variants and export the generated Android packages (*.apk)
    task exportAll_apk(type: Copy, dependsOn: ['assemble']) {
        group "Export"
        with export_apk_copySpec
        into "${rootProject.ext.apkExportDirectory}"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // External libraries - the libraries will be build and added to the .apk
    implementation project(":${Deps.freetype2_ModuleName}")
    implementation project(":${Deps.sdl2_ModuleName}")
}
